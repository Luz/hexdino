// Simple keybindings should be put silent rules
del			= _{ "d" }

// Variable intentionally creating inner token for easy access
amount			= { ASCII_DIGIT* }

movement		= _{ down | up | left | right | bottom | top | start | end }
down			= { ASCII_DIGIT* ~ &("j" | "[B") }
up			= { ASCII_DIGIT* ~ &("k" | "[A") }
left			= { ASCII_DIGIT* ~ &("h" | "[D") }
right			= { ASCII_DIGIT* ~ &("l" | "[C") }
bottom			= { ASCII_DIGIT* ~ &"G" }
top			= { ASCII_DIGIT* ~ &"gg" }
start			= { "0" }
end			= { "$" }

replace			= { "r" }
replacement		= { &replace ~ ANY ~ (anything_but_escape) }
remove			= { ASCII_DIGIT* ~ &"x" }
remove_left		= { del ~ amount ~ &"h" }
remove_right		= { del ~ amount ~ &"l" }
dd			= { ASCII_DIGIT* ~ &"dd" }
bigd			= { "D" }
insert			= { "i" }
insertstuff		= { &insert ~ ANY ~ (anything_but_escape)+ }
insertend		= { &insert ~ ANY ~ (anything_but_escape)* ~ escape_char }
jumpascii		= { "J" }
querry			= { "?" }
repeat			= { "." }

quickstuffescaped	= _{ insertend | insertstuff | insert | remove | remove_left | remove_right | dd | bigd | jumpascii | querry | repeat }

search			= { "/" }
searchstr		= { (!(escape_char | NEWLINE) ~ ANY)* }
searchend		= { &search ~ ANY ~ searchstr ~ NEWLINE }
hex_digit		= @{ ASCII_HEX_DIGIT | 'x'..'x' | 'X'..'X' }
hexsearchend		= { &search ~ ANY ~ hex_digit* ~ NEWLINE }
quickstuff		= _{ replacement | replace | hexsearchend | searchend }

saveandexit		= { (":" ~ ("wq!" | "wq") ~ NEWLINE) | "ZZ" } // careful, notice priority
exit			= { (":" ~ ("q!" | "q") ~ NEWLINE) | "ZQ" } // careful, notice priority
save			= { (":" ~ ("write!" | "write" | "w!" | "w") ~ NEWLINE) } // careful, notice priority
cmd			= _{ ( saveandexit | exit | save ) }

// Escape character definition:
escape_char		= _{ "\u{1b}" }
// Escape character not present:
anything_but_escape	= _{ !(escape_char) ~ ANY }
// Escape thought to be used as fallback to terminate all other operations
escape			= { (anything_but_escape)* ~ escape_char }

// consume any "not backspaces" until first backspace
backspace_char		= _{ "\u{7f}" }
backspace		= { (!(backspace_char) ~ ANY)* ~ backspace_char }

// Not escape and not Enter
gatherone		= _{ !(escape_char | NEWLINE) ~ ANY }
gatherall		= { gatherone* }

cmd_list		= _{ ( movement | quickstuffescaped | escape | quickstuff | backspace | cmd | gatherall ) }

