movement		= _{ down | up | left | right | start | end | bottom }
down			= { "j" | "[B" }
up			= { "k" | "[A" }
left			= { "h" | "[D" }
right			= { "l" | "[C" }
start			= { "0" }
end			= { "$" }
bottom			= { "G" }

// Do not allow escape as replacement char
replacement		= { !"\u{1b}" ~ ANY }
replaceby		= { &replace ~ ANY ~ replacement }
replace			= { "r" }
remove			= { "x" }
dd_lines		= { ('0'..'9')* }
dd			= { dd_lines ~ "dd" }
insert			= { "i" }
insertstuff		= { &insert ~ ANY ~ (anything_but_escape)+ }
insertend		= { &insert ~ ANY ~ (anything_but_escape)* ~ "\u{1b}" }
jumpascii		= { "J" }
helpfile		= { "?" }
repeat			= { "." }
searchstr		= { (!("\u{1b}" | "\n") ~ ANY)* }
search			= { "/" ~ searchstr ~ "\n" }
hex_digit		= @{ '0'..'9' | 'a'..'f' | 'A'..'F' | 'x'..'x' | 'X'..'X' }
searchbytes		= { ( hex_digit{2} )* }
hexsearch		= { "/" ~ searchbytes ~ "\n" }
gg_line			= { ('0'..'9')* }
gg			= { gg_line ~ "gg" }
quickstuffescaped	= _{ insertend | insertstuff | insert }
quickstuff		= _{ replaceby | replace | remove | dd | jumpascii | helpfile | repeat | hexsearch | search | gg }

saveandexit		= { (":" ~ ("wq!" | "wq") ~ "\n") | "ZZ" } // careful, notice priority
exit			= { (":" ~ ("q!" | "q") ~ "\n") | "ZQ" } // careful, notice priority
save			= { (":" ~ ("write!" | "write" | "w!" | "w") ~ "\n") } // careful, notice priority
cmd			= _{ ( saveandexit | exit | save ) }

// Escape character definition:
escape_char		= _{ "\u{1b}" }
// Escape character not present:
anything_but_escape	= _{ !(escape_char) ~ ANY }
// Escape thought to be used as fallback to terminate all other operations
escape			= { (anything_but_escape)* ~ escape_char }

// consume any "not backspaces" until first backspace
backspace		= { (!("\u{7f}") ~ ANY)* ~ "\u{7f}" }

// Not escape and not Enter
gatherone		= @{ !("\u{1b}" | "\n") ~ ANY }
gatherall		= { gatherone* }

cmd_list		= _{ ( movement | quickstuffescaped | escape | quickstuff | backspace | cmd | gatherall ) }

